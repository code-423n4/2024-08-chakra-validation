# 1. `receive_cross_chain_callback` Function Always Returns True
Reference: https://github.com/code-423n4/2024-08-chakra/blob/d0d45ae1d26ca1b87034e67180fac07ce9642fd9/solidity/handler/contracts/ChakraSettlementHandler.sol#L365

### Description:
The function `receive_cross_chain_callback` of the `ChakraSettlementHandler` contract always returns `true`, even when the status of the cross-chain message is `Failed`. Although the current implementation of the `settlement/ChakraSettlement` contract does not rely heavily on this return value and does not present a major vulnerability, future implementations could introduce logic that depends on accurate success or failure signals from this function. In such cases, this behavior could lead to more severe issues,

### Proof of Concept:
The function `receive_cross_chain_callback` will always return true because of how it is structured:

- **Processing Cross-Chain Message Status:**
If the status is `Success`, the function updates the transaction status to `Settled` and burns tokens if the settlement mode is `MintBurn`.
If the status is `Failed`, it updates the transaction status to `Failed`.

- **Return Statement:**
Regardless of whether the status is `Success` or `Failed`, the function proceeds to the last line, which contains `return true;`.
This return statement is unconditional, meaning it will always execute, returning `true` even in scenarios where the cross-chain message status is `Failed`.

```
function receive_cross_chain_callback(
    uint256 txid,
    string memory from_chain,
    uint256 from_handler,
    CrossChainMsgStatus status,
    uint8 sign_type,
    bytes calldata signatures
) external onlySettlement returns (bool) {
    if (is_valid_handler(from_chain, from_handler) == false) {
        return false;
    }

    require(create_cross_txs[txid].status == CrossChainTxStatus.Pending, "Invalid CrossChainTxStatus");

    if (status == CrossChainMsgStatus.Success) {
        if (mode == SettlementMode.MintBurn) {
            _erc20_burn(address(this), create_cross_txs[txid].amount);
        }
        create_cross_txs[txid].status = CrossChainTxStatus.Settled;
    }

    if (status == CrossChainMsgStatus.Failed) {
        create_cross_txs[txid].status = CrossChainTxStatus.Failed;
    }

    return true;  // This always returns true, even on failure.
}
```
This behavior is **unintended**, as indicated by the comments for the relevant function:
```solidity
/**
 * @dev Receives a cross-chain callback
 * @param txid The transaction ID
 * @param from_chain The source chain
 * @param from_handler The source handler
 * @param status The status of the cross-chain message
 * @return bool True if successful, false otherwise
 */
```
### Recommendations:

The function should return `false` if the message status is `Failed` (`status == CrossChainMsgStatus.Failed`). This issue does not represent any major problem for the the current implementation of the `ChakraSettlement` contract, the contract which is using this function. 
Consider updating the return value to reflect the actual success or failure of the message.

# 2. Incorrect Parameter Order in `CrossChainHandleResult` Event Emission
Reference: https://github.com/code-423n4/2024-08-chakra/blob/d0d45ae1d26ca1b87034e67180fac07ce9642fd9/solidity/settlement/contracts/ChakraSettlement.sol#L235

### Description:
The function `receive_cross_chain_msg` emits the `CrossChainHandleResult` event with the wrong order of parameters, mixing up from_chain and to_chain. This can lead to inaccurate data being emitted, potentially causing issues for off-chain services or systems relying on these events to track cross-chain transactions. In particular, the from_chain and to_chain values are switched in the emitted event.

### Proof of Concept:
The `CrossChainHandleResult` event is emitting its parameters in the wrong order compared to the expected event signature. Specifically:

The event is expected to emit the following parameters in this order:

```solidity
event CrossChainHandleResult(
    uint256 indexed txid,
    CrossChainMsgStatus status,
    string from_chain,
    string to_chain,
    address from_handler,
    uint256 to_handler,
    PayloadType payload_type
);
```
However, it is currently emitting in the following incorrect order:

```solidity
emit CrossChainHandleResult(
    txid,
    status,
    contract_chain_name, // Should be 'from_chain'
    from_chain,          // Should be 'to_chain` in this case `contract_chain_name`
    address(to_handler), // Should be 'from_handler'
    from_handler,        // Should be 'to_handler'
    payload_type
);
```
### Recommendations:
Correct the order of the parameters in the emit `CrossChainHandleResult` statement to align with the event definition.

# 3. Redundant Check in receive_cross_chain_msg
Reference: https://github.com/code-423n4/2024-08-chakra/blob/d0d45ae1d26ca1b87034e67180fac07ce9642fd9/solidity/handler/contracts/ChakraSettlementHandler.sol#L316

### Description:
The function `receive_cross_chain_msg` has two lines that check the same condition `(payload_type == PayloadType.ERC20)`. The require statement ensures the `payload_type` is valid (`ERC20`), making the subsequent if statement redundant. This duplication results in unnecessary checks that may add confusion during code maintenance and auditing.

### Proof of Concept:
The redundant check can be seen in the following code of the `ChakraSettlementHandler` contract:

```
require(isValidPayloadType(payload_type), "Invalid payload type");

if (payload_type == PayloadType.ERC20) {  // Redundant check
    // Logic for handling ERC20 cross-chain transfers...
}
```
Where the `isValidPayloadType()` function logic is : 
```solidity
function isValidPayloadType(
        PayloadType payload_type
    ) internal pure returns (bool) {
        return (payload_type == PayloadType.ERC20);
    }
```
The `require(isValidPayloadType(payload_type))` line ensures that `payload_type` is valid (`ERC20`), making the if check for `payload_type == PayloadType.ERC20` unnecessary.

### Recommendations:
Consider removing one of the statements checking the `payload_type` since it is already validated two times. This will simplify the code and reduce redundancy.

# 4. Typo in `deocde_transfer()` Function Name
Reference:https://github.com/code-423n4/2024-08-chakra/blob/d0d45ae1d26ca1b87034e67180fac07ce9642fd9/solidity/handler/contracts/ERC20CodecV1.sol#L65

### Description:
In the contract `ERC20CodecV1`, the function `deocde_transfer()` contains a typo in its name. This should be `decode_transfer()` to maintain consistency with standard naming conventions and prevent confusion during code use or maintenance. Misnaming functions can lead to harder-to-read code and potential integration issues.

### Proof of Concept:
The following mistyped function can be found bellow: 

```
function deocde_transfer(
    bytes calldata _payload
) external pure returns (ERC20TransferPayload memory transferPayload) {
    // Decoding logic here...
}
```
### Recommendations:
Rename the function to `decode_transfer()` for proper spelling and to adhere to common naming conventions, which will improve readability and maintainability.
